//--------------------------------------------------
//  なでしこパッド専用　チャットクライアント Ver.4
//--------------------------------------------------
//   定数「NPWSサーバ」に，なでしこパッド専用の
//   WebSocketサーバのURIが入っています。
//   なでしこの「WS接続」等の命令に対応した，単純なエコーサーバです。
//
//   2024.9.6 画像を送信する処理を加えました。画像をインラインURLに変換して送信しています。
// 

// 画面の準備
//WS切断。
送信文エディタ＝空のエディタ作成。
送信ボタン＝「文章を送信」のボタン作成。
送信画像＝白箱を絵追加。
画像選択ボタン＝「画像を選ぶ」のボタン作成。
送信画像データ＝空。
画像送信ボタン＝「画像を送信」のボタン作成。
画像送信ボタンを無効化。
送信ボタンを無効化。
終了ボタン＝「通信終了」のボタン作成。
改行作成。改行作成。

// 名前を設定する
名前＝「あなたの名前を入力」と尋ねる。
もし，名前＝空ならば終わる。
「{名前}さん，こんにちは。」と紫色で表示。

// 合言葉を設定する
合言葉＝「合言葉を入力」と尋ねる。
もし，合言葉＝空ならば終わる。
「{合言葉}に接続します。」と紫色で表示。

// サーバとの通信準備
// メッセージを受信したときの処理
WS受信時には
    S=対象をJSONデコード。
    送信者＝S@"name"。
    送信元＝S@"IP"。
    送信時刻＝S@"time"。
    受信文＝S@"data"。

　　//受信文が画像データだったら，画像を表示する処理
 	もし，（受信文の10文字左部分）＝「data:image」ならば
		// 画像を表示
		「{送信者}({送信元} / {送信時刻})> 」のラベル作成。
		受信文を絵追加。
		改行作成。
	違えば
　　　　//◆改良ポイント１
		// 文字を表示
		「{送信者}({送信元} / {送信時刻})> {受信文}」を表示。
	ここまで。

　　//◆改良ポイント２




ここまで

WSエラー発生した時には
　　「サーバに接続できませんでした」を赤色で表示。
　　終わる。
ここまで。

WS接続完了した時には
　　「サーバに接続しました。必ず通信終了ボタンを押して，サーバと切断してください。」と青色で表示。
　　送信ボタンを有効化。
　　改行作成。
ここまで。

// サーバと切断する
終了ボタンをクリックした時には
　　WS切断。
　　「サーバとの接続を切断しました。」と青色で表示。
　　送信ボタンを無効化。
　　画像送信ボタンを無効化。
ここまで。

// メッセージを送信するときの処理
送信ボタンをクリックした時には
　　文章＝送信文エディタのテキスト取得。
　　もし，文章＝空ならば，戻る。
　　
　　情報＝{}。
　　情報@"action"＝"sendmessage"。
　　情報@"name"＝名前。
　　情報@"data"＝文章。
　　//◆改良ポイント３



　　送信データ＝情報をJSONエンコード整形。
　　送信データをWS送信。
　　
　　送信文エディタに，空をテキスト設定。
ここまで。

// 送りたい画像を選択するときの処理
画像選択ボタンをクリックした時には
　　結果＝送信画像に絵ファイル選択読込。
　　もし，（結果＝ＯＫ）かつ（WS未接続＝いいえ）ならば，画像送信ボタンを有効化。
ここまで。

// 画像を送信するときの処理
画像送信ボタンをクリックした時には
　　文章＝送信画像を絵URL変換。
　　もし，文章＝空ならば，戻る。
　　
　　情報＝{}。
　　情報@"action"＝"sendmessage"。
　　情報@"name"＝名前。
　　情報@"data"＝文章。
   
　　送信データ＝情報をJSONエンコード整形。
　　送信データをWS送信。
ここまで。

// サーバと接続する
もし，WS未接続ならば
　　「{NPWSサーバ}?roomId={合言葉}」へWS接続。
ここまで。
