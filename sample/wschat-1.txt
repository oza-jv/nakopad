# ★★★　以下のURLを書き換えてください★★★
サーバURL=『wss://a0tklsq8jk.execute-api.ap-northeast-1.amazonaws.com/test』

// 名前を設定する
名前＝「あなたの名前を入力」と尋ねる。
もし，名前＝空ならば終わる。
「{名前}さん，こんにちは。」と表示。

// 画面の準備
送信文＝空のエディタ作成。
送信ボタン＝「送信」のボタン作成。
終了ボタン＝「終了」のボタン作成。
改行作成。

// サーバとの通信準備
// メッセージを受信したときの処理
WS受信時には
    S=対象をJSONデコード。
　　「{S@"name"}({S@"time"})> {S@"data"}」を表示。
ここまで

WSエラー発生した時には
　　「サーバに接続できませんでした」を表示。
　　終わる。
ここまで。

WS接続完了した時には
　　「サーバに接続しました。」と表示。
　　「必ず終了ボタンを押して，サーバと切断してください。」と表示。
ここまで。

// サーバと接続する
サーバURLへWS接続。

// サーバと切断する
終了ボタンをクリックした時には
　　WS切断。
　　「サーバとの接続を切断しました。」と表示。
ここまで。

// メッセージを送信するときの処理
送信ボタンをクリックした時には
    T=送信文のテキスト取得。
　　もし，T＝空ならば，戻る。
    情報 = {}
    情報@"action" = "sendmessage"
    情報@"name" = 名前。
    情報@"data" = T。
    S=情報をJSONエンコード整形。
　　SをWS送信。
　　送信文に，空をテキスト設定。
ここまで。

